#Maintainer: Oleg Rakhmanov <orakhmanov [at] gmail [dot] com>

pkgname=ajenti
pkgver=1.2.22.17 # found under ajenti/__init__.py
pkgrel=1
_commit=09496c82b9dbf14c7a6cc7af98f0114447bb40e1

pkgdesc="An easy server administration frontend."
arch=('any')
url="http://ajenti.org/"
license=('LGPL')
depends=('python2' 'python2-gevent' 'python2-gevent-socketio'
  'python2-lxml' 'python2-passlib' 'python2-psutil' 'python2-daemon'
  'python2-catcher' 'python2-reconfigure' 'python2-pyopenssl'
  'python2-pillow' 'python2-exconsole') 
makedepends=('python2' 'coffee-script' 'lessc' 'yuicompressor' 'git')
optdepends=('python2-dbus: services plugin dependency' 'python2-ldap: LDAP and Active Directory user synchronization support')
source=("https://github.com/Eugeny/$pkgname/archive/${_commit}.tar.gz"
        "ajenti.install"
        "ajenti.service"
        "fix-systemd.patch")

md5sums=('80e6240a9dbf07bfdbc85b31658bac17'
         '075c899d7c3725ee53ed479b24ff0588'
         'e5ddc4392334e0ff4cc7590bc8e4bf1e'
         '13c9c0d3e14d626a11b1db239f9d3248')

backup=('etc/ajenti/config.json')

prepare() {
  cd "$srcdir/$pkgname-$_commit"

  # Revert commit f6167e92d1be92901c97567f7f53a59b12418eb1
  # https://github.com/Eugeny/ajenti/commit/f6167e92d1be92901c97567f7f53a59b12418eb1
  # Fixes to systemd services show up.
  patch -Np1 -i ${srcdir}/fix-systemd.patch

  msg2 'Replacing python shebang by python2 shebang...'
  find . -type f -exec sed -i \
    -e'1s|^#!/usr/bin/env python$|#!/usr/bin/env python2|' \
    -e '1s|^#!/usr/bin/python$|#!/usr/bin/env python2|' \
    "{}" \;
  sed -i -e '1s/`which python`/python2/' Makefile

#  msg2 'Replacing yui-compressor by yuicompressor...'
#  find . -type f -exec sed -i -e's/yui-compressor/yuicompressor/' "{}" \;
}

build() {
  cd "$srcdir/$pkgname-$_commit"

  msg2 'Build tgz archive'
  make tgz

  cd "$srcdir/$pkgname-$_commit/dist"
  tar xzf $pkgname-$pkgver.tar.gz
}

package() {
  cd "$srcdir/$pkgname-$_commit/dist/$pkgname-$pkgver"
  
  python2 setup.py install --prefix=/usr --root=$pkgdir
 
  # Fix config file permissions
  chmod o-rwx "$pkgdir/etc/ajenti/config.json"

  rm -r $pkgdir/etc/init.d

  install -D $srcdir/ajenti.service $pkgdir/usr/lib/systemd/system/ajenti.service
}
