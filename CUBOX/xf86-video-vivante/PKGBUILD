# Xorg driver for VIVANTE GPU
# Maintainer: RaumZeit <raumzeit@gmx.net>
# Contributor: Oleg Rakhmanov <orakhmanov [at] gmail>
#              Add exports for LD and C Flags

buildarch=4

pkgname=xf86-video-vivante
_yocto_pkgname=xserver-xorg-video-imx-viv
pkgver=3.10.17_1.0.0
_pkgver=${pkgver/_/-}
pkgrel=4
pkgdesc="Xorg driver for proprietary VIVANTE GPU drivers"

url="https://community.freescale.com/"
arch=('armv7h')
license=('MIT')
makedepends=('xorg-server-devel')
depends=('libdrm-viv' 'libx11' 'gpu-viv-bin-mx6q-x11=3.10.17_1.0.0')
source=("http://www.freescale.com/lgfiles/NMG/MAD/YOCTO/${_yocto_pkgname}-${_pkgver}.tar.gz"
        'libdrm.patch'
        'fb.patch'
        '10-vivante.conf')
md5sums=( '697d9a3fb244eb95eae4207bf2d9c321'
          'f7077ac0dcb884dbecb296e7faf2c20a'
          '2cf4b02e3f8741539a7b5ac94a80594f'
          'a9b630f51e3b618ce30befb1ffc96bcb')


prepare() {
  cd "${_yocto_pkgname}-${_pkgver}"
  patch -p1 < "${srcdir}"/libdrm.patch
  patch -p1 < "${srcdir}"/fb.patch
  cp "${srcdir}"/10-vivante.conf ./
}

build() {
export CFLAGS="$CFLAGS -I/opt/fsl/include/"
export CXXFLAGS="$CXXFLAGS -I/opt/fsl/include/"
export LDFLAGS="$LDFLAGS -L/opt/fsl/lib/"
export LFLAGS="$LDFLAGS"

  cd "${_yocto_pkgname}-${_pkgver}"
  make -C EXA/src -f makefile.linux \
  BUSID_HAS_NUMBER=1 \
  XSERVER_GREATER_THAN_13=1 \
  BUILD_HARD_VFP=1

  make -C FslExt/src/ -f makefile.linux \
  BUSID_HAS_NUMBER=1 \
  XSERVER_GREATER_THAN_13=1 \
  BUILD_HARD_VFP=1

  make -C util/autohdmi/ -f makefile.linux \
  BUSID_HAS_NUMBER=1 \
  XSERVER_GREATER_THAN_13=1 \
  BUILD_HARD_VFP=1
}

package() {
  cd "${_yocto_pkgname}-${_pkgver}"
  mkdir -p "${pkgdir}/usr/lib/xorg/modules/drivers"
  mkdir -p "${pkgdir}/usr/include"
  mkdir -p "${pkgdir}/usr/sbin"
  mkdir -p "${pkgdir}/etc/X11/xorg.conf.d/"
  cp -axr EXA/src/vivante_drv.so "${pkgdir}/usr/lib/xorg/modules/drivers/"
  cp -axr EXA/src/vivante_gal/vivante_priv.h "${pkgdir}/usr/include/"
  cp -axr EXA/src/vivante_gal/vivante_gal.h "${pkgdir}/usr/include/"
  cp -axr FslExt/src/libfsl_x11_ext.so "${pkgdir}/usr/lib/"
  cp -axr util/autohdmi/autohdmi "${pkgdir}/usr/sbin/"
  cp -axr 10-vivante.conf "${pkgdir}/etc/X11/xorg.conf.d/"

  find "${pkgdir}/usr/include/" -type f -exec chmod 660 {} \;
}

# vim:set ts=2 sw=2 et:
