# Maintainer: Oleg Rakhmanov <orakhmanov [at] gmail [dot] com>
# Contributors: Charles Ghislain, Guillaume ALAUX, Daniel J Griffiths, Jason Chu, Geoffroy Carrier, Army, kfgz, Thomas Dziedzic, Dan Serban, jjacky, EasySly

###########################################################################################################
#Note: 	Oracle does not allow for redistribution for JRE, on top of this,
#	to get the embedded version one needs to register.
#
#	To build this package YOU need to provide the source tarball availe here:
#	http://www.oracle.com/technetwork/java/embedded/downloads/javase/index.html
#
#	Armv6h and Armv7h: "ARMv6/7 Linux - Headless - Client Compiler EABI, VFP, HardFP ABI, Little Endian"
#	(Last I checked it was at the top of the ARM list)
#
#	Armv5: "ARMv5 Linux - Headless EABI, SoftFP ABI, Little Endian"
#	(Last I checked it was at the bottom of the ARM list)
#	
#	Put the tarball in the same directory with PKGBUILD and run makepkg
###########################################################################################################

pkgname=ejre-arm-headless
_major=7
_minor=51
_build=13
_date=18_dec_2013


_pkgver=${_major}u${_minor}
pkgver=${_major}.${_minor}
pkgrel=1
pkgdesc="Oracle Embedded Java Runtime Environment for ARM - headless"
arch=('arm' 'armv6h' 'armv7h')
url=http://www.oracle.com/technetwork/java/javase/downloads/index.html
license=('custom')
depends=('shared-mime-info' 'xdg-utils')
optdepends=('alsa-lib: sound')
provides=("java-runtime=$_major" "java-runtime-headless=$_major")
conflicts=("${provides[@]}")
backup=('etc/profile.d/ejre.csh'
        'etc/profile.d/ejre.sh')
install=ejre-arm-headless.install

if [ "$CARCH" == "armv6h" -o "$CARCH" == "armv7h" ]; then
   _source=ejre-${_major}u${_minor}-fcs-b${_build}-linux-arm-vfp-hflt-client_headless-${_date}.tar.gz
else
   _source=ejre-${_major}u${_minor}-fcs-b${_build}-linux-arm-sflt-headless-${_date}.tar.gz
fi

source=("${_source}"
        'ejre.csh'
        'ejre.sh')

md5sums=('fba114c4848c864afcea71198aebfc90'
         '6fdb3e968f76bdc56cbe7bdb720e3dcc'
         'fccc55f6ac29f16dd08d5ac167b80ee4')

[ "$CARCH" = 'armv6h' ] && md5sums[0]='9b00967670f564d518486b42d12c3077'

package() {
  msg2 "Creating required dirs"
  cd ejre1.$_major.0_$_minor
  mkdir -p "$pkgdir"/{opt/java/ejre,usr/share/licenses/ejre,etc/profile.d}

  msg2 "Moving stuff in place"
  mv COPYRIGHT *.txt "$pkgdir"/usr/share/licenses/ejre/
  mv * "$pkgdir"/opt/java/ejre/

  msg2 "Installing the scripts, confs of our own"
  cd "$srcdir"
  install -m755 ejre.{,c}sh "$pkgdir"/etc/profile.d/
}
