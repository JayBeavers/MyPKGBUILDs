# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=20

pkgname=raspberrypi-firmware
pkgver=20191022
pkgrel=1
_commit=8062d68f49cb54c8cea3c1c9ae3b30d799d23352
pkgdesc="Firmware tools, libraries, and headers for Raspberry Pi"
arch=('aarch64')
url="https://github.com/raspberrypi/userland"
license=('custom')
makedepends=('cmake')
replaces=('raspberrypi-firmware-tools')
provides=('raspberrypi-firmware-tools')
options=(!strip)
source=("https://github.com/raspberrypi/userland/archive/${_commit}.tar.gz"
        "https://raw.githubusercontent.com/raspberrypi/firmware/master/opt/vc/LICENCE"
        '00-raspberrypi-firmware.conf')
md5sums=('8c0ce4984985f800091427f20b86bfb7'
         '86e53f5f5909ee66900418028de11780'
         '72e0d5818fc513ece1b964f25f7e7882')

build() {
  cd ${srcdir}/userland-${_commit}

  if [ "${CARCH}" == 'aarch64' ]; then
    cmake -DCMAKE_BUILD_TYPE=Release -DARM64=ON
  else
    cmake -DCMAKE_BUILD_TYPE=Release
  fi
  
  make
}

package() {
  mkdir -p "${pkgdir}"/opt/vc

  cd ${srcdir}/userland-${_commit}
  make install DESTDIR="${pkgdir}"
  cp ../LICENCE "${pkgdir}"/opt/vc

  # Create lib links
  mkdir -p "${pkgdir}"/etc/ld.so.conf.d/
  cp "${srcdir}/00-raspberrypi-firmware.conf" "${pkgdir}"/etc/ld.so.conf.d/
}
